apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

/*
 * CopyrightÂ© 2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

/************************************************************************************
 * Packaging section
 */
task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}
if (tasks.getAsMap()['groovydoc']) {
    task javadocJar(type: Jar, dependsOn: [tasks.getAsMap()['groovydoc']]) {
        classifier 'javadoc'
        from tasks.getAsMap()['groovydoc'].destinationDir
    }
} else {
    task javadocJar(type: Jar, dependsOn: [javadoc]) {
        classifier 'javadoc'
        from javadoc.destinationDir
    }
}

tasks.withType(Jar) {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Gradle-Version": "${gradle.gradleVersion}",
                "Extension-Name": "${archivesBaseName}",
                "Build-Jdk": "${JavaVersion.current().toString()}",
                "Url": "${websiteUrl}",
                "Name": "${projectId}".replaceAll("\\.", "/"),
                "Specification-Title": "${group}",
                "Specification-Vendor": "${organization}",
                "Specification-Version": "${version}",
                "Implementation-Vendor": "${organization}",
                "Implementation-Title": "${group}.${archivesBaseName}",
                "Implementation-Version": "${version}",
                "License": "${license}",
        )
    }
    from("LICENSE") {
        into "META-INF"
    }
}
artifacts {
    archives sourcesJar
    archives javadocJar
}


bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = "${project.archivesBaseName}"
        desc = "${project.description}"
        userOrg = "${project.organization}"
        licenses = ["${project.license}"]
        vcsUrl = "${project.vcsUrl}"
        websiteUrl = "${project.websiteUrl}"
        issueTrackerUrl = "${project.issueTrackerUrl}"
        githubRepo = "${project.githubRepository}" //Optional Github repositoryName
        githubReleaseNotesFile = 'README.md'
        labels = project.tags
        version {
            name = project.version
            vcsTag = project.version
        }
    }
}